apply plugin: 'maven-publish'

apply from: "${mavPubishConfigFilePath}/utils.gradle"
apply from: "${mavPubishConfigFilePath}/artifact-javadoc-handler.gradle"
apply from: "${mavPubishConfigFilePath}/artifact-pom-manager.gradle"

project.afterEvaluate {
    publishing {
        publications {
            if (isAndroidProject()) {
                android.libraryVariants.all { variant ->
                    "maven${variant.name.capitalize()}Publish"(MavenPublication) {
                        artifact project.tasks.findByName("bundle${variant.name.capitalize()}Aar")
                        artifactId getStringProperty("mavArtifactId")
                        groupId getStringProperty("mavGroupId")
                        version getStringProperty("mavVersion")
                        artifact androidJavadocsJar
                        artifact androidSourcesJar
                    }
                }
            } else {
                mavenPublish(MavenPublication) {
                    artifact jar
                    artifact sourcesJar
                    artifact javadocJar
                }

                decoratePom(pom)
            }
        }

        repositories {
            def isToRemoteRepo = getBooleanProperty("mavPublishToRemoteRepo")
            def isToInternalRepo = getBooleanProperty("mavPublishToInternalRepo")
            def isToMavenLocal = getBooleanProperty("mavPublishToMavenLocal")

            if (isToRemoteRepo) {
                maven {
                    credentials {
                        username getStringProperty("mavRemoteRepoUser")
                        password getStringProperty("mavRemoteRepoPassword")
                    }
                    url = getStringProperty("mavRepoRemoteUrl")
                }
            }

            if (isToInternalRepo) {
                maven {
                    url = getStringProperty("mavRepoInternalUrl")
                }
            }

            if (isToMavenLocal || (!isToInternalRepo && !isToRemoteRepo)) {
                mavenLocal()
            }
        }
    }
}
